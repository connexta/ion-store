plugins {
	id "org.springframework.boot" version "2.1.5.RELEASE"
	id "com.palantir.docker" version "0.22.1"
}

dependencies {
	implementation "org.springframework.data:spring-data-solr:4.0.8.RELEASE"
	implementation("org.springframework.data:spring-data-cassandra:2.1.5.RELEASE") {
		exclude group: "com.google.guava", module: "guava"
		because "CVE-2018-10237 in guava 19.0"
	}
	implementation("com.google.guava:guava:27.1-jre") {
		because "CVE-2018-10237 in guava 19.0"
	}
	testImplementation "org.testcontainers:testcontainers:1.11.2"
}

bootJar {
	launchScript()
}

docker {
	files tasks.bootJar.outputs
	name "cnxta/cdr-multi-int-store:${project.version}"
	buildArgs([JAR_FILE: "${tasks.bootJar.outputs.files.singleFile.name}"])
}

build.dependsOn("docker")
