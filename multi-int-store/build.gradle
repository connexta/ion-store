/*
 * Copyright (c) 2019 Connexta, LLC
 *
 * Released under the GNU Lesser General Public License version 3; see
 * https://www.gnu.org/licenses/lgpl-3.0.html
 */
/* Build Script */
plugins {
    id "org.springframework.boot"
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file("src/itest/java")
        }
        resources.srcDir file("src/itest/resources")
    }
}

configurations {
    implementation {
        exclude group: "org.jdom" // CVE-2019-12814
    }
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation(Dependencies.awsS3())
    implementation(Dependencies.springBootStarterWeb())
    implementation(Dependencies.zookeeper()) {
        force = true
        because "Zookeeper 3.4.14 is a transitive dependency of org.springframework.data:spring-data-solr:4.0.9.RELEASE and is affected by CVE-2019-0232."
    }
    implementation(Dependencies.springDataSolr())
    integrationTestImplementation(Dependencies.springBootStarterTest())
    integrationTestImplementation(Dependencies.testContainers())
    integrationTestImplementation(Dependencies.restito())
    integrationTestImplementation(Dependencies.hamcrestOptional())
    integrationTestImplementation(Dependencies.springDataSolr())
    integrationTestImplementation(Dependencies.awsS3())
    integrationTestImplementation(Dependencies.springBootStarterWeb())
}

dependencyCheck {
    suppressionFile = "${projectDir}/owasp-suppressions.xml"
}

bootJar {
    launchScript()
}

docker {
    files tasks.bootJar.outputs
    name "cnxta/cdr-multi-int-store:${project.version}"
    buildArgs([JAR_FILE: "${tasks.bootJar.outputs.files.singleFile.name}"])
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

integrationTest.onlyIf { !project.hasProperty("skipITests") }

check.dependsOn integrationTest
bootRun.dependsOn build
integrationTest.mustRunAfter test

assemble.finalizedBy("docker")
build.finalizedBy("docker")
