/*
 * Copyright (c) 2019 Connexta, LLC
 *
 * Released under the GNU Lesser General Public License version 3; see
 * https://www.gnu.org/licenses/lgpl-3.0.html
 */
/* Build Script */
plugins {
    id "org.springframework.boot"
}

configurations {
    implementation {
        exclude group: "org.jdom" // CVE-2019-12814
    }
}

dependencies {
    implementation(Dependencies.springBootStarterWeb())
    implementation(Dependencies.reactiveStreams())
    implementation(Dependencies.commonsIO())
    implementation(Dependencies.ingestAPI())
    implementation(Dependencies.transformAPI())
    testImplementation(Dependencies.springBootStarterTest())
}

processResources {
    expand([transformApiVersion: Versions.transformAPI])
}

dependencyCheck {
    suppressionFile = "${projectDir}/owasp-suppressions.xml"
}

springBoot {
    mainClassName = "com.connexta.ingest.IngestApplication"
}

bootJar {
    launchScript()
}

docker {
    files tasks.bootJar.outputs
    name "cnxta/cdr-ingest:${project.version}"
    buildArgs([JAR_FILE: "${tasks.bootJar.outputs.files.singleFile.name}"])
}

build.finalizedBy("docker")
bootRun.dependsOn(build)
