/*
 * Copyright (c) 2019 Connexta, LLC
 *
 * Released under the GNU Lesser General Public License version 3; see
 * https://www.gnu.org/licenses/lgpl-3.0.html
 */
/* Build Script */
plugins {
    id "org.springframework.boot"
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file("src/itest/java")
        }
        resources.srcDir file("src/itest/resources")
    }
}

configurations {
    implementation {
        exclude group: "org.jdom" // CVE-2019-12814
    }
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation(Dependencies.springBootStarterWeb())
    implementation(Dependencies.reactiveStreams())
    implementation(Dependencies.commonsIO())
    implementation(Dependencies.ingestAPI())
    implementation(Dependencies.transformAPI())

    testImplementation(Dependencies.springBootStarterTest())

    integrationTestImplementation Dependencies.ingestAPI()
    integrationTestImplementation Dependencies.transformAPI()
    integrationTestImplementation Dependencies.springBootStarterTest()
    integrationTestImplementation Dependencies.springBootStarterWeb()
    integrationTestImplementation(Dependencies.reactiveStreams())
}

processResources {
    expand([transformApiVersion: Versions.transformAPI])
}

dependencyCheck {
    suppressionFile = "${projectDir}/owasp-suppressions.xml"
}

springBoot {
    mainClassName = "com.connexta.ingest.IngestApplication"
}

bootJar {
    launchScript()
}

docker {
    files tasks.bootJar.outputs
    name "cnxta/cdr-ingest:${project.version}"
    buildArgs([JAR_FILE: "${tasks.bootJar.outputs.files.singleFile.name}"])
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
bootRun.dependsOn build
integrationTest.mustRunAfter test

build.finalizedBy("docker")
assemble.finalizedBy("docker")

