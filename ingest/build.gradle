/*
 * Copyright (c) 2019 Connexta, LLC
 *
 * Released under the GNU Lesser General Public License version 3; see
 * https://www.gnu.org/licenses/lgpl-3.0.html
 */
/* Build Script */
plugins {
    id "org.springframework.boot"
}

sourceSets {
    itest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file("src/itest/java")
        }
        resources.srcDir file("src/itest/resources")
    }
}

configurations {
    implementation {
        exclude group: "org.jdom" // CVE-2019-12814
    }
    itestCompile.extendsFrom(testCompile)
    itestRuntime.extendsFrom(testRuntime)
}

dependencies {
    implementation(Dependencies.springBootStarterWeb())
    implementation(Dependencies.reactiveStreams())
    implementation(Dependencies.commonsIO())
    implementation(Dependencies.ingestAPI())
    implementation(Dependencies.transformAPI())

    testImplementation(Dependencies.springBootStarterTest())

    itestImplementation(Dependencies.ingestAPI())
    itestImplementation(Dependencies.transformAPI())
    itestImplementation(Dependencies.springBootStarterTest())
    itestImplementation(Dependencies.springBootStarterWeb())
    itestImplementation(Dependencies.reactiveStreams())
}

processResources {
    expand([transformApiVersion: Versions.transformAPI])
}

dependencyCheck {
    suppressionFile = "${projectDir}/owasp-suppressions.xml"
}

springBoot {
    mainClassName = "com.connexta.ingest.IngestApplication"
}

bootJar {
    launchScript()
}

docker {
    files tasks.bootJar.outputs
    name "cnxta/cdr-ingest:${project.version}"
    buildArgs([JAR_FILE: "${tasks.bootJar.outputs.files.singleFile.name}"])
}

task integrationTest(type: Test) {
    onlyIf { !project.hasProperty("skipITests") }
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
    outputs.upToDateWhen { false }
    shouldRunAfter test
}

check.dependsOn integrationTest
bootRun.dependsOn build

build.finalizedBy("docker")
assemble.finalizedBy("docker")
