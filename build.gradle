/*
 * Copyright (c) 2019 Connexta, LLC
 *
 * Released under the GNU Lesser General Public License version 3; see
 * https://www.gnu.org/licenses/lgpl-3.0.html
 */
/* Build Script */
buildscript {
    ext {
        indexApiVersion = "0.4.0"
        junitJupiterVersion = "5.5.2"
        lombokVersion = "1.18.10"
        springBootVersion = "2.2.1.RELEASE"
        springfoxSwaggerVersion = "2.9.2"
        storeApiVersion = "0.4.0"
        transformApiVersion = "0.2.0"
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.26.0"
    id "java"
    id "com.palantir.docker" version "0.22.1"
    id "maven"
    id "org.owasp.dependencycheck" version "5.2.4"
    id "org.springframework.boot" version "${springBootVersion}"
    id "org.sonarqube" version "2.8"
}

group = "com.connexta.store"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://artifacts.codice.org/content/repositories/ion-releases/" }
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    implementation "com.amazonaws:aws-java-sdk-s3:1.11.613"
    implementation "com.connexta.ingest:ingest-api-rest-spring-stubs:0.6.0"
    implementation "com.connexta.transformation:transformation-api-rest-spring-stubs:${transformApiVersion}"
    implementation "com.connexta.search:index-api-rest-spring-stubs:${indexApiVersion}"
    implementation "com.connexta.store:store-api-rest-spring-stubs:${storeApiVersion}"
    implementation "com.dyngr:polling:1.1.3"
    implementation "io.springfox:springfox-swagger2:${springfoxSwaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}"
    implementation "javax.inject:javax.inject:1"
    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "org.springframework.data:spring-data-solr:4.1.2.RELEASE"
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testCompile "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testCompile "cloud.localstack:localstack-utils:0.1.22"
    testCompile "org.mockito:mockito-junit-jupiter:3.1.0"
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") {
        exclude group: "junit", module: "junit" // excludes JUnit 4
    }
    testCompile "org.testcontainers:junit-jupiter:1.12.3"
    testImplementation "org.springframework.boot:spring-boot-starter-webflux:${springBootVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

spotless {
    File licenseFile = rootProject.file("license.java")
    format "misc", SpotlessConfig.getMisc()
    java SpotlessConfig.getJava(licenseFile)
    groovyGradle SpotlessConfig.getGroovy(licenseFile)
}

test {
    useJUnitPlatform()
}

ext.configArgs = [
        "parallel"  : project.hasProperty("parallel"),
        "skipITests": project.hasProperty("skipITests"),
]

configurations.all {
    resolutionStrategy {
        force "com.fasterxml.jackson.core:jackson-databind:[2.9.9.3,)"
        force "com.google.guava:guava:[24.1.1,)"
    }
}

dependencyCheck {
    failBuildOnCVSS = 4
    failOnError = true

    analyzers {
        ossIndexEnabled = false
    }

    // Add support for NVD mirror
    if (project.hasProperty("dependencyCheckUrlModified") && project.hasProperty("dependencyCheckUrlBase")) {
        println "Using NVD Mirrors: ${dependencyCheckUrlBase} and ${dependencyCheckUrlModified}"
        cve {
            urlModified = "${dependencyCheckUrlModified}"
            urlBase = "${dependencyCheckUrlBase}"
        }
    }

    suppressionFile = "${projectDir}/owasp-suppressions.xml"
}

processResources {
    expand([indexApiVersion: "${indexApiVersion}", storeApiVersion: "${storeApiVersion}", transformApiVersion: "${transformApiVersion}"])
}

bootJar {
    launchScript()
}

docker {
    files tasks.bootJar.outputs
    name "cnxta/ion-store"
    buildArgs([JAR_FILE: "${tasks.bootJar.outputs.files.singleFile.name}"])
}

tasks.withType(Test) {
    //  Forces Gradle to always run the tests, regardless if they are up to date or not.
    outputs.upToDateWhen { false }

    if (project.configArgs["skipITests"]) {
        exclude "**/*ITests*"
    }
}

tasks.withType(Test, TestLogger.configs(project.configArgs))

task listDependencies(type: DependencyReportTask)

task deploy(type: Exec) {
    dependsOn build
    commandLine "groovy", "deploy.groovy"
}

task deployK8s(type: Exec) {
    dependsOn build
    commandLine "groovy", "deploy-k8s.groovy"
}

assemble.finalizedBy("docker")
build.finalizedBy("docker")
bootRun.dependsOn(build)
