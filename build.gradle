/*
 * Copyright (c) 2019 Connexta, LLC
 *
 * Released under the GNU Lesser General Public License version 3; see
 * https://www.gnu.org/licenses/lgpl-3.0.html
 */
/* Build Script */
buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.24.3"
    id "java"
    id "com.palantir.docker" version "0.22.1"
    id "maven"
    id "org.owasp.dependencycheck" version "5.2.2"
    id "org.springframework.boot" version "2.1.8.RELEASE"
    id "org.sonarqube" version "2.8"
}

group = "com.connexta.store"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://artifacts.codice.org/content/repositories/ion-releases/" }
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:1.18.10"
    compileOnly "org.projectlombok:lombok:1.18.8"
    implementation "com.amazonaws:aws-java-sdk-s3:1.11.613"
    implementation "com.connexta.store:store-api-rest-spring-stubs:0.2.0"
    implementation "io.springfox:springfox-swagger2:2.9.2"
    implementation "io.springfox:springfox-swagger-ui:2.9.2"
    implementation "javax.inject:javax.inject:1"
    implementation "org.springframework.boot:spring-boot-starter-actuator:2.1.8.RELEASE"
    implementation "org.springframework.boot:spring-boot-starter-web:2.1.8.RELEASE"
    implementation "org.springframework.data:spring-data-solr:4.1.0.RELEASE"
    testCompile "org.junit.jupiter:junit-jupiter-params:5.5.2"
    testCompile "org.mockito:mockito-core:3.0.0"
    testCompile "org.testcontainers:junit-jupiter:1.12.2"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.2"
    testImplementation("org.springframework.boot:spring-boot-starter-test:2.1.8.RELEASE") {
        exclude group: "junit", module: "junit" // excludes JUnit 4
    }
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.5.2"
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

spotless {
    File licenseFile = rootProject.file("license.java")
    format "misc", SpotlessConfig.getMisc()
    java SpotlessConfig.getJava(licenseFile)
    groovyGradle SpotlessConfig.getGroovy(licenseFile)
}

test {
    useJUnitPlatform()
}

ext.configArgs = [
        "parallel"  : project.hasProperty("parallel"),
        "skipITests": project.hasProperty("skipITests"),
]

configurations.all {
    resolutionStrategy {
        force "com.fasterxml.jackson.core:jackson-databind:[2.9.9.3,)"
        force "com.google.guava:guava:[24.1.1,)"
    }
}

dependencyCheck {
    failBuildOnCVSS = 4
    failOnError = true

    analyzers {
        ossIndexEnabled = false
    }

    // Add support for NVD mirror
    if (project.hasProperty("dependencyCheckUrlModified") && project.hasProperty("dependencyCheckUrlBase")) {
        println "Using NVD Mirrors: ${dependencyCheckUrlBase} and ${dependencyCheckUrlModified}"
        cve {
            urlModified = "${dependencyCheckUrlModified}"
            urlBase = "${dependencyCheckUrlBase}"
        }
    }

    suppressionFile = "${projectDir}/owasp-suppressions.xml"
}

bootJar {
    launchScript()
}

docker {
    files tasks.bootJar.outputs
    name "cnxta/ion-store"
    buildArgs([JAR_FILE: "${tasks.bootJar.outputs.files.singleFile.name}"])
}

tasks.withType(Test) {
    //  Forces Gradle to always run the tests, regardless if they are up to date or not.
    outputs.upToDateWhen { false }

    if (project.configArgs["skipITests"]) {
        exclude "**/*ITests*"
    }
}

tasks.withType(Test, TestLogger.configs(project.configArgs))

task listDependencies(type: DependencyReportTask)

task deploy(type: Exec) {
    dependsOn build
    commandLine "./deploy.bash"
}

assemble.finalizedBy("docker")
build.finalizedBy("docker")
bootRun.dependsOn(build)
